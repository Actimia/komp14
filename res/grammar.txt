Program     -> MainClass ClassDecls

MainClass   -> class id { public static void main ( String [ ] id ) { VarDecls Statements } }

ClassDecls  -> ClassDecl ClassDecls
            ->

ClassDecl   -> class id { VarDecls MethodDecls }

VarDecls    -> VarDecl VarDecls
            ->

VarDecl     -> Type id ;

MethodDecls -> MethodDecl MethodDecls
            ->

MethodDecl  -> public Type id ( FormalList ) { VarDecls Statements return Expression ; }

FormalList  -> Type id FormalRest
            ->

FormalRest  -> , Type id FormalRest
            ->

Type        -> int [ ]
            -> boolean
            -> int
            -> id

Statements  -> Statement Statements
            ->

Statement   -> { Statements }
            -> if ( Expression ) Statement else Statement
            -> while ( Expression ) Statement
            -> System.out.println ( Expression ) ;
            -> id = Expression ;
            -> id [ Expression ] = Expression ;

Expression  -> Expression Operation Expression
            -> Expression [ Expression ]
            -> Expression . length
            -> Expression . id ( ExpList )
            -> intlit
            -> true
            -> false
            -> id
            -> this
            -> new int [ Expression ]
            -> new id ( )
            -> ! Expression
            -> ( Expression )

Operation   -> &&
            -> <
            -> +
            -> âˆ’
            -> *

ExpList     -> Expression ExpRest
            ->

ExpRest     -> , Expression ExpRest
            ->
